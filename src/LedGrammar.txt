program(prog PE) ::= prog_el(PE)
program(prog PE cut_root(P)) ::= prog_el(PE) program(P)

term(T) ::= lPar term(T) rPar
term(V) ::= variable(V)
term(T) ::= tuple(T)
term(S) ::= set(S)
term(I) ::= interval(I)
term(LE) ::= lam_ex(LE)
term(boolT BT) ::= bool_term(BT)
term(ar AT) ::= ar_term(AT)
//todo tup index
term_s(terms T) ::= term(T)
term_s(terms T cut_root(Ts)) ::= term(T) comma term_s(Ts)

variable(vName I) ::= id(I)

var_s(vars V) ::= variable(V)
var_s(vars V cut_root(Vs)) ::= variable(V) comma var_s(Vs)

tuple(tup Ts) ::= lPar term_s(Ts) rPar

set(st) ::= lBrace rBrace
set(st Ts) ::= lBrace term_s(Ts) rBrace

interval(nrval N1 N2) ::= lBrace ar_term(N1) twoDot ar_term(N2) rBrace

lam_ex(lamEx Vs T) ::= lam var_s(Vs) dot term(T)

bool_term(I) ::= equivalence(I)

equivalence(I) ::= implication(I)
equivalence(equiv I E) ::= implication(I) ifF equivalence(E)

implication(D) ::= disjunction(D)
implication(impl D I) ::= disjunction(D) implies implication(I)

disjunction(C) ::= conjunction(C)
disjunction(disj D C) ::= disjunction(D) or conjunction(C)

conjunction(N) ::= negation(N)
conjunction(conj C N) ::= conjunction(C) amperstand negation(N)

negation(IBT) ::= inner_bool_term(IBT)
negation(neg IBT) ::= tilde inner_bool_term(IBT)

inner_bool_term(V) ::= variable(V)
inner_bool_term(T) ::= tru(T)
inner_bool_term(comp C) ::= compare(C)
inner_bool_term(PBT) ::= par_bool_term(PBT)

par_bool_term(BT) ::= lPar bool_term(BT) rPar

compare(eq AT1 AT2) ::= ar_term(AT1) equal ar_term(AT2)
compare(less AT1 AT2) ::= ar_term(AT1) lessThan ar_term(AT2)
compare(greater AT1 AT2) ::= ar_term(AT1) greaterThan ar_term(AT2)
compare(lessEq AT1 AT2) ::= ar_term(AT1) lessOrEq ar_term(AT2)
compare(greaterEq AT1 AT2) ::= ar_term(AT1) greaterOrEq ar_term(AT2)

ar_term(BS) ::= bi_sum(BS)

bi_sum(BP) ::= bi_prod(BP)
bi_sum(add BS BP) ::= bi_sum(BS) plus bi_prod(BP)
bi_sum(biMinus BS BP) ::= bi_sum(BS) minus bi_prod(BP)

bi_prod(ST) ::= sign_term(ST)
bi_prod(mult BP ST) ::= bi_prod(BP) multiplied sign_term(ST)
bi_prod(div BP ST) ::= bi_prod(BP) divided sign_term(ST)
bi_prod(md BP ST) ::= bi_prod(BP) mod sign_term(ST)

sign_term(E) ::= expo(E)
sign_term(E) ::= plus expo(E)
sign_term(uMinus E) ::= minus expo(E)

expo(IAT) ::= inner_ar_term(IAT)
expo(exp IAT E) ::= inner_ar_term(IAT) caret expo(E)

inner_ar_term(V) ::= variable(V)
inner_ar_term(N) ::= num(N)
inner_ar_term(abs AT) ::= bar ar_term(AT) bar
inner_ar_term(flr PAT) ::= floor par_ar_term(PAT)
inner_ar_term(clng PAT) ::= ceil par_ar_term(PAT)
inner_ar_term(PAT) ::= par_ar_term(PAT)

par_ar_term(AT) ::= lPar ar_term(AT) rPar

var_in_term(varInT V T) ::= variable(V) in term(T)

var_s_in_term(varsInT Vs T) ::= var_s(Vs) in term(T)

var_s_in_term_s(varsInTs VsIT) ::= var_s_in_term(VsIT)
var_s_in_term_s(varsInTs VsIT cut_root(VsITs)) ::= var_s_in_term(VsIT) comma var_s_in_term_s(VsITs)

var_decl(varDecl VIT) ::= var var_in_term(VIT) diamond
var_decl(varDecl VsITs) ::= vars var_s_in_term_s(VsITs) diamond

f_name(fName I) ::= id(I)

def_head(defHead FN) ::= f_name(FN)
def_head(defHead FN Vs) ::= f_name(FN) lPar var_s(Vs) rPar

f_def(fDef DH T) ::= def def_head(DH) equal term(T) diamond

prog_el(VD) ::= var_decl(VD)
prog_el(FD) ::= f_def(FD)
