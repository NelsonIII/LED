prog_el_s(progEls T) ::= prog_el(T)
prog_el_s(progEls T cut_root(Ts)) ::= prog_el(T) prog_el_s(Ts)

prog_el(T) ::= hashIsGame(T)
prog_el(T) ::= def_(T)

def_(funDef T) ::= fun_def(T)
def_(relDef T) ::= rel_def(T)

fun_def(funDefNoWhere T T2) ::= fun_term(T) colonEq def_body(T2)
fun_def(funDefWhere T T2 T3) ::= fun_term(T) colonEq def_body(T2) where bool_term(T3)

rel_def(relDefNoWhere T T2) ::= rel_term(T) iff def_body(T2)
rel_def(relDefWhere T T2 T3) ::= rel_term(T) iff def_body(T2) where bool_term(T3)

def_body(T) ::= term(T)
def_body(T) ::= term_if_bool_term_s(T)
def_body(T) ::= term_if_bool_term_s_other(T)

term(T) ::= nonbool_term(T)
term(T) ::= bool_term(T)

nonbool_term(T) ::= atom_term(T)
nonbool_term(T) ::= string_term(T)
nonbool_term(T) ::= tuple_term(T)
nonbool_term(T) ::= tuple_indexed_term(T)
nonbool_term(T) ::= set_term(T)
nonbool_term(T) ::= ar_term(T)
nonbool_term(T) ::= inner_term(T)

inner_term(T) ::= lParenth term(T) rParenth
inner_term(T) ::= user_term(T)

atom_term(T) ::= atom_term_1(T)

atom_term_1(T) ::= user_term(T)
atom_term_1(T) ::= atom(T)

string_term(T) ::= string_term_1(T)

string_term_1(T) ::= user_term(T)
string_term_1(T) ::= string(T)

tuple_term(T) ::= tuple_term_1(T)

tuple_term_1(plusOp T T2) ::= tuple_term_1(T) plus tuple_term_2(T2)
tuple_term_1(T) ::= tuple_term_2(T)

tuple_term_2(T) ::= user_term(T)
tuple_term_2(tpl T) ::= lParenth term_s(T) rParenth
tuple_term_2(T) ::= tuple_indexed_term(T)

tuple_indexed_term(T) ::= tuple_indexed_term_1(T)

tuple_indexed_term_1(T) ::= user_term(T)
tuple_indexed_term_1(tuIn T T2) ::= tuple_term(T) lBracket ar_term(T2) rBracket
tuple_indexed_term_1(tuSl T T2 T3) ::= tuple_term(T) lBracket ar_term(T2) twoDots ar_term(T3) rBracket

set_term(T) ::= set_term_1(T)

set_term_1(unn T T2) ::= set_term_1(T) U set_term_2(T2)
set_term_1(diff T T2) ::= set_term_1(T) backslash set_term_2(T2)
set_term_1(T) ::= set_term_2(T)

set_term_2(nrsec T T2) ::= set_term_2(T) sec set_term_3(T2)
set_term_2(starOp T T2) ::= set_term_2(T) star set_term_3(T2)
set_term_2(T) ::= set_term_3(T)

set_term_3(aggrUnn T T2) ::= U lBracket bool_term(T) rBracket set_term_3(T2)
set_term_3(aggrNrsec T T2) ::= Sec lBracket bool_term(T) rBracket set_term_3(T2)
set_term_3(T) ::= inner_set_term(T)

inner_set_term(T) ::= lParenth set_term(T) rParenth
inner_set_term(T) ::= user_term(T)
inner_set_term(powSet T) ::= Pow lParenth set_term(T) rParenth
inner_set_term(T) ::= brace_set(T)

brace_set(set) ::= lBrace rBrace
brace_set(set T) ::= lBrace term_s(T) rBrace
brace_set(iv T T2) ::= lBrace ar_term(T) twoDots ar_term(T2) rBrace
brace_set(setCompr T T2) ::= lBrace term(T) pipe bool_term(T2) rBrace

bool_term(T) ::= bool_term_1(T)

bool_term_1(equiv T T2) ::= bool_term_2(T) equivalent bool_term_1(T2)
bool_term_1(T) ::= bool_term_2(T)

bool_term_2(impl T T2) ::= bool_term_3(T) implies bool_term_2(T2)
bool_term_2(T) ::= bool_term_3(T)

bool_term_3(disj T T2) ::= bool_term_3(T) opDisj bool_term_4(T2)
bool_term_3(T) ::= bool_term_4(T)

bool_term_4(conj T T2) ::= bool_term_4(T) opConj bool_term_5(T2)
bool_term_4(T) ::= bool_term_5(T)

bool_term_5(neg T) ::= tilde bool_term_5(T)
bool_term_5(T) ::= bool_term_6(T)

bool_term_6(T) ::= quantified_term(T)
bool_term_6(T) ::= set_bool_term(T)
bool_term_6(T) ::= equality(T)
bool_term_6(T) ::= inner_bool_term(T)

inner_bool_term(T) ::= lParenth bool_term(T) rParenth
inner_bool_term(T) ::= user_term(T)
inner_bool_term(T) ::= truth(T)

quantified_term(exist T T2) ::= some symbol_s_in_set_term(T) dot bool_term_6(T2)
quantified_term(univ T T2) ::= all symbol_s_in_set_term(T) dot bool_term_6(T2)

set_bool_term(sbset T T2) ::= set_term(T) sub set_term(T2)
set_bool_term(setMem T T2) ::= set_mem_arg(T) in set_term(T2)

set_mem_arg(T) ::= nonbool_term(T)
set_mem_arg(T) ::= inner_bool_term(T)

equality(eq T T2) ::= equality_arg(T) equal equality_arg(T2)
equality(uneq T T2) ::= equality_arg(T) unequal equality_arg(T2)
equality(T) ::= relational(T)

equality_arg(T) ::= nonbool_term(T)
equality_arg(T) ::= inner_bool_term(T)

relational(less T T2) ::= ar_term(T) lArrow ar_term(T2)
relational(greater T T2) ::= ar_term(T) rArrow ar_term(T2)
relational(lessEq T T2) ::= ar_term(T) lessOrEq ar_term(T2)
relational(greaterEq T T2) ::= ar_term(T) greaterOrEq ar_term(T2)

ar_term(T) ::= ar_term_1(T)

ar_term_1(plusOp T T2) ::= ar_term_1(T) plus ar_term_2(T2)
ar_term_1(bMns T T2) ::= ar_term_1(T) minus ar_term_2(T2)
ar_term_1(T) ::= ar_term_2(T)

ar_term_2(starOp T T2) ::= ar_term_2(T) star ar_term_3(T2)
ar_term_2(div T T2) ::= ar_term_2(T) slash ar_term_3(T2)
ar_term_2(md T T2) ::= ar_term_2(T) mod ar_term_3(T2)
ar_term_2(T) ::= ar_term_3(T)

ar_term_3(aggrSum T T2) ::= Sum lBracket bool_term(T) rBracket ar_term_3(T2)
ar_term_3(aggrProd T T2) ::= Prod lBracket bool_term(T) rBracket ar_term_3(T2)
ar_term_3(uMns T) ::= minus ar_term_3(T)
ar_term_3(T) ::= plus ar_term_3(T)
ar_term_3(T) ::= ar_term_4(T)

ar_term_4(exp T T2) ::= inner_ar_term(T) hat ar_term_3(T2)
ar_term_4(T) ::= inner_ar_term(T)

inner_ar_term(T) ::= lParenth ar_term(T) rParenth
inner_ar_term(T) ::= user_term(T)
inner_ar_term(N) ::= numl(N)
inner_ar_term(flr T) ::= floor lParenth ar_term(T) rParenth
inner_ar_term(clng T) ::= ceil lParenth ar_term(T) rParenth
inner_ar_term(pipesOp T) ::= pipe pipes_arg(T) pipe

pipes_arg(T) ::= ar_term(T)
pipes_arg(T) ::= set_term(T)
pipes_arg(T) ::= tuple_term(T)

term_s(terms T) ::= term(T)
term_s(terms T cut_root(Ts)) ::= term(T) comma term_s(Ts)

symbol_s(syms T) ::= symbol_(T)
symbol_s(syms T cut_root(Ts)) ::= symbol_(T) comma symbol_s(Ts)

symbol_s_in_set_term(symsInSet T T2) ::= symbol_s(T) in set_term(T2)

term_if_bool_term(tIfBT T T2) ::= term(T) if bool_term(T2)

term_if_bool_term_s(tIfBTs T) ::= term_if_bool_term(T)
term_if_bool_term_s(tIfBTs T cut_root(Ts)) ::= term_if_bool_term(T) semicolon term_if_bool_term_s(Ts)

term_other(tOther T) ::= term(T) otherwise

term_if_bool_term_s_other(tIfBTsO T T2) ::= term_if_bool_term_s(T) semicolon term_other(T2)

fun_term(funT T) ::= fun_name(T)
fun_term(funT T T2) ::= fun_name(T) lParenth symbol_s(T2) rParenth

rel_term(relT T) ::= rel_name(T)
rel_term(relT T T2) ::= rel_name(T) lParenth symbol_s(T2) rParenth

symbol_(symN T) ::= id(T)
fun_name(funN T) ::= id(T)
rel_name(relN T) ::= id(T)

user_term(symOrNullFunRel T) ::= id(T)
user_term(nonnullFunRel T T2) ::= id(T) lParenth term_s(T2) rParenth
